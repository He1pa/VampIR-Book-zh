\section{Programming in \vampir}

\subsection{Basic Arithmetic} \label{ARITH}

\vampir's arithmetic denotations are fairly straightforward, being very similar to what's present in many mainstream languages.

\vampir\ provides four different methods to denote numbers. One can use

\begin{itemize}
  \item standard decimal notation
  \item hexadecimal notation, so long as the number is prefixed by \lstinline{0x}
  \item octal notation, so long as the number is prefixed by \lstinline{0o}
  \item binary notation, so long as the number is prefixed by \lstinline{0b}
\end{itemize}

The following example program demonstrates all the number notations.

\begin{lstlisting}[language=Python]
  0xee9a592ba9a9518 = 1074572035719075096;
  1074572035719075096 = 0o73515131127246512430;
  0o73515131127246512430 =
  0b111011101001101001011001001010111010100110101001010100011000;
\end{lstlisting}

Notice that \vampir\ allows for many top-level equations in the same program.

One can add, subtract, and multiply numbers, using, respectively, the infix operators \lstinline{+}, \lstinline{-}, and \lstinline{*}.

\begin{lstlisting}[language=Python]
  20 * 3 - 20 = 20 + 20;
\end{lstlisting}

\vampir\ uses a standard order of operations where multiplication takes precedence over addition and subtraction, and everything is left-associated otherwise. Further clarification can be made using parentheses.

\begin{lstlisting}[language=Python]
  1 + 2 + 3 - 4 * 5 - 6 + 7 =
  ((((1 + 2) + 3) - (4 * 5)) - 6) + 7;
\end{lstlisting}

\vampir\ also allows for division in arithmetic circuits, denoted with the infix operator \lstinline{/}.

\begin{lstlisting}[language=Python]
  15/3 = 5;
\end{lstlisting}

Division has the same precedence as multiplication, and associates to the left, otherwise. 

\begin{lstlisting}[language=Python]
  2/3*4/5 = ((2/3)*4)/5;
\end{lstlisting}

This operation will give an output using the division of the underlying field, meaning constraints may differ in truth value depending on the compilation target. If one uses a prime field of size

52435875175126190479447740508185965837690552500527637822603658699938581184513,

used, for example, in BLS12-381, then the following is valid.

\begin{lstlisting}[language=Python]
2/3 = 0x26a48d1bb889d46d66689d580335f2ac713f36abaaaa1eaa5555555500000001;
\end{lstlisting}
  
If one tries dividing by zero, they will get an error at some point in the process depending on where the division occurs. \lstinline{1/0 = 1;} will produce an error during type inference. \lstinline{1/x = 1;} will produce an error if `x' is set to 0 during witness solicitation.

\vampir\ also possesses a single unary operation in the form of negation, denoted with \lstinline{-}. Negation must be surrounded by parentheses unless it's iterated with additional negations.

\begin{lstlisting}[language=Python]
  (-10) = 0 - 10;
  0 - 10 = (---10);
\end{lstlisting}

Exponentiation is also available via the infix operator \lstinline{^}.

\begin{lstlisting}[language=Python]
  2 ^ 2 = 4;
\end{lstlisting}

Exponentiation has greater precedence than any other arithmetic operator. 

\begin{lstlisting}[language=Python]
  5 * 6 ^ 7 = 5 * (6 ^ 7);
\end{lstlisting}

Only constant exponents are allowed, as they can be translated into iterated multiplication at circuit compilation time. \lstinline{2 ^ x = 4;} will create an error during type inference.

Exponentiation by 0 will always produce 1, even if the base is 0.

\begin{lstlisting}[language=Python]
  2 ^ 0 = 1;
  0 ^ 0 = 1;
  0 ^ 2 = 0;
\end{lstlisting}

\textcolor{red}{[Note: this section may describe a bug instead of intended behaviour]}

Exponentiation by negatives is a bit weird. They are offset by 1 from what one might expect.

\begin{lstlisting}[language=Python]
  2 ^ (-5) = 1 / 2 ^ 4;
  6 ^ (-2) = 1 / 6;
  5 ^ (-3) = 1 / 5 ^ 2;
\end{lstlisting}

Exponentiating by -1 will always produce 1, with the exception of 0.

\begin{lstlisting}[language=Python]
  2 ^ (-1) = 1;
  6 ^ (-1) = 1;
  5 ^ (-1) = 1;
  0 ^ (-1) = 0;
\end{lstlisting}

\subsection{Functions} \label{FUN}

The \lstinline{def} construct allows one to define, not just constants, but functions as well.

\begin{lstlisting}[language=Python]
  def square x = x ^ 2;

  square 4 = 16;
\end{lstlisting}

The name of the function is denoted by the first token after \lstinline{def}. Function application is denoted by juxtaposition, a la some functional languages such as Haskell.

During compilation, the type checker will identify \lstinline{square}'s type as a function from integers to integers.

\begin{lstlisting}[language=bash]
  > square[3]: (int -> int)
\end{lstlisting}

Functions can have as many arguments as you want.

\begin{lstlisting}[language=Python]
  def f x y z = x * z + y;

  f 4 5 6 = 29;
\end{lstlisting}

The type of this function indicates multiple arguments.

\begin{lstlisting}[language=bash]
  > f[5]: (int -> (int -> (int -> int)))
\end{lstlisting}

This tells us that each argument will take in an integer and produce a function, until the third argument, after which it will produce an integer. This indicates that application is left-associative and functions are curried by default (more on this in section \ref{HOF}).

\begin{lstlisting}[language=Python]
  ((f 4) 5) 6 = f 4 5 6;
\end{lstlisting}

Definitions can contain definitions inside of them.

\begin{lstlisting}[language=Python]
  def g x = {
    def k = 20;
    x * k
  };

  g 3 = 60;
\end{lstlisting}

Notice the usage of curly braces to define blocks that scope the function. Also, notice that the semicolon for the definition comes after the closing curly brace, and there is no semicolon for the last line before that brace. 

Internal \lstinline{def} statements should be used similarly to \lstinline{let} statements common in many functional languages. Internal \lstinline{def} statements can be functions as well.

\begin{lstlisting}[language=Python]
  def cube x = {
    def square x = x * x;
    x * square x
  };

  cube 3 = 27;
\end{lstlisting}

Types for internal \lstinline{def} statements are not given during compilation, but they are still checked.

There is no limit to the number of lines or nestings within a definition. 

\begin{lstlisting}[language=Python]
  def power x = {
    def hypercube x = {
      def square x = x * x;
      square (square x)
    };
    def cube x = {
      def square x = x * x;
      x * square x
    };
    cube (hypercube x)
   };

  power 2 = 4096;
\end{lstlisting}

Overlapping names are allowed. References to names will refer to the most recent binding.

\begin{lstlisting}[language=Python]
  def x = 4;
  def x = 8;
  x = 8;
\end{lstlisting}

During type checking, both variables are listed.

\begin{lstlisting}[language=bash]
  > x[2]: int
  > x[3]: int
\end{lstlisting}

The numbers next to the names are, in some sense, the ``true'' names of the variables. Each variable has a unique number identifying it, allowing the system to avoid confusing variables with the same name.

\label{FSTUNIT}
Equations can also appear in definitions. Definitions may or may not return a value and can act as gates for other values.

\begin{lstlisting}[language=Python]
  def g1 x = {x = 4; x};
  g1 4;

  def g2 x = {x = 10};
  g2 10;
\end{lstlisting}

Looking at the types, one sees something interesting. 

\begin{lstlisting}[language=bash]
  > g2[5]: (int -> ())
\end{lstlisting}

\lstinline{g2} does, in fact, return a value; something of type \lstinline{()}. This will be explained in section \ref{UNITEXP}.

This illustrates that a \vampir\ file does not need any top-level equations, but will still represent a proposition through the equations enforced by function calls. \lstinline{g1 5;} will produce an invalid proof, in this case.

\label{CALLREF}

Equations aren't enforced if their parent function is never actually called.

\begin{lstlisting}[language=Python]
  def j x = {0 = 1; x};
  
  1 = 1;
\end{lstlisting}

will produce a valid proof because `j' is never fully instantiated. Equations become part of the circuit when their parent function is fully instantiated. \lstinline|def k = {0 = 1; 4};| \textbf{will} produce an invalid proof since `k' is already fully instantiated.

Using polynomial constraints, one can create sophisticated, reusable checks. The following, for example, checks that a field element is a boolean; either 0 or 1.

\begin{lstlisting}[language=Python]
  def isBool x = { 
    (x - 1) * x = 0;
    x
  };
  
  isBool 0;
  isBool 1;
\end{lstlisting}

\label{ARITHEX}
There are a handful of additional arithmetic operators which may be used so long as the circuit can be transformed so they aren't referenced. These operators will be detailed in full in section \ref{EXAR}. For now, the modulus, \lstinline|%|, will be used as an example.

\begin{lstlisting}[language=Python]
  6 = 15 % 9;
\end{lstlisting}

will create a valid proof. This is because \lstinline|15 % 9| can be immediately simplified into \lstinline|6| during circuit generation. If one attempts to use an uninstantiated variable, that is one without a value defined in the file, they will get an error.

\begin{lstlisting}[language=Python]
  6 = x % 9;
\end{lstlisting}

will generate an error during circuit creation informing us that the modulus is not a supported constraint.


\subsection{Tuples} \label{TUP}

One of the few first-order data structures provided in \vampir\ is the tuple. One can use them in place of numbers to check multiple things in parallel.

\begin{lstlisting}[language=Python]
  def xs = (1, 2);
  
  xs = (1, 2);
\end{lstlisting}

During type checking, \lstinline{xs}'s type is given as

\begin{lstlisting}[language=bash]
  > xs: (int, int)
\end{lstlisting}

This indicates that \lstinline{xs} is a pair of integers. One can also iterate tuples as much as they want.

\begin{lstlisting}[language=Python]
  def xs = (1, 2, 3, 4, 5, 6, 7, 8, 9);
  
  xs = (1, 2, 3, 4, 5, 6, 7, 8, 9);
\end{lstlisting}

During type checking, one can observe the type

\begin{lstlisting}[language=bash]
  > xs[2]: (int, (int, (int, (int, (int, (int, (int, (int, int))))))))
\end{lstlisting}

This indicates that tuples with more than two entries are, internally, just nested pairs associated to the right.

\begin{lstlisting}[language=Python]
  (1, 2, 3) = (1, (2, 3));
\end{lstlisting}

\lstinline{(1, 2, 3) = ((1, 2), 3);} will produce a type error. One cannot compare tuples to numbers, so \lstinline{1 = (1, 1);} will produce an error. One also cannot treat tuples as numbers, so, for example, \lstinline{(1, 2) + (3, 4);} will produce an error.

\vampir\ provides the ability to define functions over tuples using pattern matching.

\begin{lstlisting}[language=Python]
  def add (a, b) (x, y) = (a + x, b + y);
  
  add (1, 2) (3, 4) = (4, 6);
\end{lstlisting}

Many standard tuple construction and manipulation functions can be defined.

\begin{lstlisting}[language=Python]
  def fst (x, y) = x;
  def snd (x, y) = y;
  def third (x, y, z) = z;
  def dup x = (x, x);
  def swap (x, y) = (y, x);
  def assoc (x, (y, z)) = ((x, y), z);
\end{lstlisting}

As you can see by \lstinline{assoc}, one can pattern match a tuple within another tuple. Additionally, the fact that n-tuples are nested pairs means that \lstinline{fst} acts uniformly on tuples; using that definition we'd have \lstinline{fst (1, 2, 3) = 1}.

The types here tell us something interesting. One sees, for example, that;

\begin{lstlisting}[language=bash]
  > fst[4]: (([24], [25]) -> [24])
  > dup[13]: ([43] -> ([43], [43]))
  > swap[16]: (([50], [51]) -> ([51], [50]))
\end{lstlisting}

The numbers in square braces denote variable types. These functions do not have a fixed type, but their types can be instantiated to be different types, depending on the context. Different calls within the same program can also have different types. For example, one can have both of these equations in the same file:

\begin{lstlisting}[language=Python]
  fst (1, 2) = 1;
  fst ((1, 2), 3) = (1, 2);
\end{lstlisting}

Even though the same function is called on both lines, different types are returned each time. On the first line, it returns an integer while on the second it returns a pair.

\label{UNITEXP}
\vampir\ also provides a 0-tuple denoted \lstinline{()}, analogous to the return value of a unit type in many functional languages or a void type in many imperative languages. It's the closest thing to getting a function to return nothing.

\begin{lstlisting}[language=Python]
  def tt = ();
  def f x = {
    x = 2;
    ()
  };

  f 2 = tt;
\end{lstlisting}

During type inference, the following is shown;

\begin{lstlisting}[language=bash]
   > tt[2]: ()
   > f[4]: (int -> ())
\end{lstlisting}

indicating that \lstinline{tt} is an element of the unit type and that \lstinline{f} will return an element of the unit type on an integer input. This shows that functions that have only equations do the same thing as functions that explicitly return a \lstinline{()}, as mentioned in section \ref{FSTUNIT}.

Using units, one can design some of our functions to act like list manipulation programs. That is if one thinks of \lstinline{(x, y)} as \lstinline{Cons x y} and \lstinline{()} as \lstinline{Nil}, they can encode a list like `[1, 2, 3, 4]' as \lstinline{(1, 2, 3, 4, ())}. The utility of this lies in the fact that each entry will appear in the head of a pair. This allows us to define more flexible programs. Modifying a few of our previous examples, one can have

\begin{lstlisting}[language=Python]
  def swap (x, y, r) = (y, x, r);
  swap (1, 2, ()) = (2, 1, ());
  swap (1, 2, 3, ()) = (2, 1, 3, ());
  swap (1, 2, 3, 4, ()) = (2, 1, 3, 4, ());

  def tail (x, y) = y;
  tail (1, 2, ()) = (2, ());
  tail (1, 2, 3, ()) = (2, 3, ());  
  tail (1, 2, 3, 4, ()) = (2, 3, 4, ());
  
  def total (x, y, r) = x+y;
  total (1, 2, ()) = 3;
  total (1, 2, 3, ()) = 3;  
  total (1, 2, 3, 4, ()) = 3;

  def add (a, b, q) (x, y, r) = (a + x, b + y, ());
  add (1, 2, ()) (3, 4, ()) = (4, 6, ());
  add (1, 2, 3, 4, 5, ()) (6, 7, 8, ()) = (7, 9, ());

  def zip (a, b, q) (x, y, r) = ((a, x), (b, y), ());
  zip (1, 2, 3, 4, 5, ()) (6, 7, 8, ()) = ((1, 6), (2, 7), ());
\end{lstlisting}

\vampir\ ultimately generates a fixed, finitely iterated data type, meaning we cannot treat iterated tuples as a proper list type. As such, many list manipulation programs that one might take for granted can't be defined in full generality using tuples. However, this technique can recover some of that functionality in practice. Proper lists are available, see section \ref{LIST}.

Witness solicitation interacts in a somewhat interesting way with pairs. If one compiles the program

\begin{lstlisting}[language=Python]
  x = (1, 2);
\end{lstlisting}

it seems like they will need to input a pair. However, during proof creation, \vampir\ will ask;

\begin{lstlisting}[language=Python]
  > * Soliciting circuit witnesses...
  > ** x.0[9] (private): 
  > ** x.1[10] (private): 
\end{lstlisting}

It splits the variable into two sub-variables, named \lstinline{x.0} (the first element of \lstinline{x}) and \lstinline{x.1} (the second element of \lstinline{x}). So the compiled circuit does not, in fact, have a hole in the shape of a pair; rather it has two holes corresponding to the elements of the pair.

Without a variable-free first-order datatype for each uninstantiated variable, circuit generation is not possible. If one tries compiling \lstinline{x = y;} without definitions for x or y, they will get an error indicating such.



\subsection{The Iter Function} \label{ITER}

Frequently, we will want to repeat a function over and over again. \vampir\ includes the \lstinline{iter} function for this exact purpose. It takes three arguments;

\begin{itemize}
\item A non-negative constant.
\item A function to iterate.
\item An argument to iterate on.
\end{itemize}

For example, if the exponential function was not already built-in, we could implement it via \lstinline{iter}.

\begin{lstlisting}[language=Python]
    def exp_rec m r = m * r;
    def exp m n = iter n (exp_rec m) 1;
    
    exp 3 2 = 9;
\end{lstlisting}

\lstinline{iter} must be able to be unfolded at circuit-generation time, meaning the constant cannot be a field element that would be decided at proving time. \lstinline{iter x f 3} for an unbound \lstinline{x} will give an error indicating that \lstinline{x} must be a constant.

The main limitation of \lstinline{iter} is that the function has to take a (simply typed) X and produce another X. If the input and output types do not match, we will get a typing error. This largely prevents useful interactions between \lstinline{iter} and tuples. For example, trying the following

\begin{lstlisting}[language=Python]
  iter 2 tail (1, 2, 3, 4, ());
\end{lstlisting}

will yield an error indicating that \lstinline{([138], [137])} cannot unify with \lstinline{[137]}. This tells us that the input and output types to \lstinline{tail} cannot be made the same; the input must be a tuple, \lstinline{([138], [137])}, while the output must be of the same type as the second element of the tuple, \lstinline{[137]}.

\subsection{Lists} \label{LIST}

\vampir\ provides built-in lists which can be manipulated in a way that's akin to lists in functional languages like Haskell or ML. The empty list is denoted \lstinline{[]} while list constructors are denoted \lstinline{:}. There is no syntax sugar for lists, so one must explicitly end them with \lstinline{[]}.

\begin{lstlisting}[language=Python]
  def exList = 1:2:3:[];

  exList = 1:2:3:[];
\end{lstlisting}

As you can see, we can check multiple things in parallel, just like with tuples. The main difference is in the type. During proof generation, we should see the following;

\begin{lstlisting}[language=bash]
  > exList: [int]
\end{lstlisting}

The square braces indicate that \lstinline{exList} is a list of \lstinline{int}s. This is, of course, not to be confused with the square braces which also surround type variables. A list of variable type will have two layers of square braces. Unlike with tuples, every entry is assumed to have the same type. This allows for type-safe interoperability with \lstinline{iter}.

\begin{lstlisting}[language=Python]
  def hd (h:t) = h;
  def tl (h:t) = t;
  def nth l n = hd (iter n tl l);

  nth (1:2:3:4:[]) 2 = 3;
\end{lstlisting}

As you can see, we can pattern match on lists just like we can with tuples. There is no way to case on lists, instead, it's up to the programmer to ensure all patterns are valid. If one tries \lstinline{hd []}, they will get an error during circuit generation indicating that the pattern \lstinline{(h : t)} cannot match \lstinline{[]}.

This does not mean, however, that one must always indicate the length of the list when recursing over a list. Instead, \vampir\ provides the \lstinline{fold} functions for recursing over lists. \lstinline{fold} is equivalent to right-folding functions, such as \lstinline{foldr}, common to functional programming languages. It takes three arguments;


\begin{itemize}
\item A base value to return on an empty list.
\item A function which incorporates an element of the list with the recursive 
\item A list to fold over.
\end{itemize}

As a basic example, we can sum over the elements of a list with

\begin{lstlisting}[language=Python]
  def plus x y = x + y;
  def sum l = fold 0 plus l;

  sum (1:2:3:4:[]) = 10;
\end{lstlisting}

With these in place, most common list manipulation functions can be defined.

\begin{lstlisting}[language=Python]
  def cons x y = x:y;
  def append xs = fold xs cons;
  
  def take_rec take (h:t) = h:(take t);
  def take n = iter n take_rec (fun x {[]});
  
  def map_rec f x acc = (f x):acc;
  def map f xs = fold xs (map_rec f) [];

  def zip_rec x z (y:ys) = (x, y):(z ys);
  def zip xs = fold xs zip_rec (fun x {[]});
  
  def zipWith_rec f x z (y:ys) = (f x y):(z ys);
  def zipWith f xs = fold xs (zipWith_rec f) (fun x {[]});

  def reverse = fold [] (fun x r { append r (x:[]) });

  def last l = hd (reverse l);
\end{lstlisting}

There are some limitations that aren't present in most languages. Lists cannot be directly compared. Instead, list comparisons will be zipped into underlying field comparisons. If this isn't possible, an error will occur as a field element will try to be compared to nothing. \lstinline{1:[] = 1:2:[]}, for example, won't create an invalid proof; rather, we'll get an error indicating the equality is simply invalid.

Unlike with tuples, one cannot use them for witness solicitation. \lstinline{x = 1:2:3:[];} for an unbounded \lstinline{x} will produce an error indicating that \lstinline{x} is an undefined list.


\subsection{Higher-Order Functions} \label{HOF}

\vampir\ has rather robust support for higher-order functions. One can manipulate functions more or less like any other value. One can save a partially instantiated function.

\begin{lstlisting}[language=Python]
  def f x y = x * y;
  def g = f 2;

  g 3 = 6;
\end{lstlisting}

Notice that \lstinline{g} is defined in terms of \lstinline{f} with only one of its two arguments applied. The types are given as

\begin{lstlisting}[language=bash]
  > f[4]: (int -> (int -> int))
  > g[5]: (int -> int)
\end{lstlisting}

one can see that \lstinline{g}'s type is exactly the return type of \lstinline{f}. This is no different than if \lstinline{f} returned an integer instead of a function.

One can also write functions that take a function as input.

\begin{lstlisting}[language=Python]
  def app2 f x = f (f x);
  def times2 x = 2 * x;

  app2 times2 3 = 12;
\end{lstlisting}

Looking at the types;

\begin{lstlisting}[language=bash]
  > f[4]: app2[4]: (([10] -> [10]) -> ([10] -> [10]))
\end{lstlisting}

one can see that \lstinline{app2} takes a function from the (variable) type \lstinline{[10]} onto itself, and creates a new function, also from \lstinline{[10]} onto itself. At this point, it may be clear that \lstinline{iter}, and, in fact, \lstinline{fold} as well, are examples of higher-order functions.

As mentioned in section \ref{CALLREF}, the constraints of a function are not called unless the function is fully instantiated. The following program produces a valid proof.

\begin{lstlisting}[language=Python]
  def f x y = {0 = 1; x};

  f 2;
\end{lstlisting}

While \lstinline{f} is called, it's not fully instantiated, and so \lstinline{0 = 1} does not become part of the generated circuit.

There are many higher-order combinators that may be useful for some applications.

\begin{lstlisting}[language=Python]
  def comp f g x = f (g x);
  def const x y = x;
  def flip f x y = f y x;
  def delta f x = f x x;
\end{lstlisting}

\vampir\ provides a notation for anonymous functions using the \lstinline{fun} keyword.

\begin{lstlisting}[language=Python]
  (fun x {x}) 15 = 15;
  (fun x y {x + y}) 15 20 = 35;
  (fun (x, y) {x + y}) (15, 3) = 18;
  flip (fun x y {x - y}) 15 3 = (-12);
\end{lstlisting}

Notice that one can bind multiple arguments and pattern match on tuples using anonymous functions. These can also be used as return values in definitions.

\begin{lstlisting}[language=Python]
  def id = fun x {x};

  id 2 = 2;
\end{lstlisting}

one can also place constraints inside anonymous functions.

\begin{lstlisting}[language=Python]
  (fun x {x = 2; x}) 2;
  (fun x {x = 5}) 5 = ();
\end{lstlisting}

as you can see, anonymous functions don't need to return data. Like \lstinline{def}s, \lstinline{fun}s that don't return a value still return a \lstinline{()}.

We cannot use functions in comparisons; only first-order data can be compared. 

\begin{lstlisting}[language=Python]
  (fun x { 2 }) = (fun x { 1 + 1 });
\end{lstlisting}

will produce an error.

One can also create functions to curry and uncurry our functions

\begin{lstlisting}[language=Python]
  def curry f x y = f (x, y);
  def uncurry f (x, y) = f x y;

  curry (fun (x, y) {x + y}) 3 4 = uncurry (fun x y {x + y}) (3, 4);
\end{lstlisting}

\vampir\ does not give unrestricted access to higher-order functions; any appearing in a program must be simply typable. For example, \lstinline|fun x { x x };| will produce a typing error. This means that, in particular, one cannot define a fixed-point combinator, which would be necessary to implement general recursion. Still, there are many powerful techniques made available by what is given. In particular, one can define and utilize lambda-encoded data.

If \vampir\ didn't already have built-in pairs, one could implement them as

\begin{lstlisting}[language=Python]
  def pair x y p = p x y;
\end{lstlisting}

By issuing different arguments to \lstinline|pair x y|, one can get the underlying components.

\begin{lstlisting}[language=Python]
  def fst p = p (fun x y {x});
  fst (pair 1 2) = 1;

  def snd p = p (fun x y {y});
  snd (pair 1 2) = 2;
\end{lstlisting}

While one cannot compare these pairs using \lstinline|=| directly, one can define a bespoke comparison function.

\begin{lstlisting}[language=Python]
  def eq p q = {
    fst p = fst q; 
    snd p = snd q
  };
  
  eq (pair 1 2) (pair 1 2);
\end{lstlisting}

Further structural manipulation functions can easily be defined.

\begin{lstlisting}[language=Python]
  def swap p = p (fun x y {pair y x});
  eq (swap (pair 1 2)) (pair 2 1);

  def assoc p = 
    fun f {p (fun x yz {yz (fun y z {
        f (pair x y) z
    })})};
  eq (fst (assoc (pair 1 (pair 2 3)))) (pair 1 2);
  snd (assoc (pair 1 (pair 2 3))) = 3;
\end{lstlisting}

Functions converting between the two formats can also easily be defined;

\begin{lstlisting}[language=Python]
  def lpair (x, y) = pair x y;
  def upair p = p (fun x y {(x, y)});
  eq (lpair (1, 2)) (pair 1 2);
  (1, 2) = upair (pair 1 2);
\end{lstlisting}

This is all interesting but doesn't give us anything that wasn't already provided. However, a variant of this idea can encode more datatypes. For example, we can encode maybe/option types;

\begin{lstlisting}[language=Python]
  def nothing n j = n;
  def just x n j = j x;

  def maybe d m = m d (fun x {x});
\end{lstlisting}

We can also encode coproducts, allowing us to emulate branching programs, at least during circuit generation.

\begin{lstlisting}[language=Python]
  def inl x l r = l x;
  def inr x l r = r x;

  def case f g c = c f g;

  case (fun x {x + 1}) (fun x {x + 2}) (inl 0) = 1;
  case (fun x {x + 1}) (fun x {x + 2}) (inr 0) = 2;
\end{lstlisting}

If they didn't already exist, we could implement lists as well through this method.

\begin{lstlisting}[language=Python]
  def nil n c = n;
  def cons x xs n c = c x (xs n c);

  def ex_list = cons 1 (cons 2 (cons 3 nil));
  // Same as
  def ex_list2 n c = c 1 (c 2 (c 3 n));
\end{lstlisting}

One can also issue arguments into the list, whose structure explicitly represents folding a function over the list.

\begin{lstlisting}[language=Python]
  ex_list 0 (fun x y {x + y}) = 6;
\end{lstlisting}

One can define many well-founded recursive functions this way.

\begin{lstlisting}[language=Python]
  def append l1 l2 = fun n c { l1 (l2 n c) c };

  def map f l n c = l n (fun x xs { c (f x) xs });
\end{lstlisting}

We can easily convert between built-in lists and encoded ones.

\begin{lstlisting}[language=Python]
  llist l = fold nil cons l;
  ulist l = l [] (fun x y {x:y});

  ulist (llist (1:2:3:4:[])) = 1:2:3:4:[];
\end{lstlisting}

This method is limited by the simple type system of \vampir. While taking the head and tail of these kinds of lists is possible without first converting to a normal list, iterating such a function cannot type check. This prevents us, for example, from defining analogs of \lstinline|nth| in a type-safe manner. Nonetheless, this technique is useful for cases where we only need to fold over a tree-like structure to produce first-order data. Any kind of well-founded tree can be encoded and folded over via similar methods. For example, binary trees can be encoded using;

\begin{lstlisting}[language=Python]
  def leaf lf br = lf;
  def branch l r lf br = br (l lf br) (r lf br);
\end{lstlisting}

This opens the door to many powerful functional programming techniques, including free monads, for example.

It should be noted that there are some fundamental limitations to this method. Mainly, it can't interact non-trivially with the underlying field. One cannot, for example, translate a field element into a Church-encoded number or a boolean. While one can certainly implement those in \vampir\ (in fact, \lstinline|app2| from earlier is a Church numeral, and \lstinline|const| is a Church boolean), they cannot be used to actually reason about field elements. The methods described in this section are exclusively useful for data \textbf{structure} manipulation, but not data manipulation in general. This is because data structures are fixed at circuit generation time, but field values are not completely fixed until proof generation, which occurs after circuit generation. This implies that the circuit's structure cannot depend on field values.